buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.7.RELEASE"
  id "com.github.johnrengelman.shadow" version "4.0.0"
  id "net.ltgt.apt-eclipse" version "0.21"
  id "net.ltgt.apt-idea" version "0.21"
  id 'com.bmuschko.docker-remote-api' version '3.6.2'
}

apply plugin: "application"
apply plugin: "java"
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "com.github.ben-manes.versions"

ext {
  lombokVersion = '1.18.6'
}

version = '0.0.6'
sourceCompatibility = 1.8
group "factorial.service"

repositories {
  jcenter()
  mavenLocal()
  mavenCentral()
  maven { url "https://jcenter.bintray.com" }
}

docker {
  registryCredentials {
    username = (System.getenv('DOCKER_BUILDER_USERNAME') ?: (project.hasProperty('dockerRegistryUsername') ? dockerRegistryUsername : '')) ?: ''
    password = (System.getenv('DOCKER_BUILDER_PASSWORD') ?: (project.hasProperty('dockerRegistryPassword') ? dockerRegistryPassword : '')) ?: ''
  }
}

dependencyManagement {
  imports {
    mavenBom 'io.micronaut:micronaut-bom:1.1.0.RC1'
  }
}

dependencies {
  compileOnly "org.projectlombok:lombok:${ lombokVersion }"
  annotationProcessor "org.projectlombok:lombok:${ lombokVersion }"
  annotationProcessor "io.micronaut:micronaut-inject-java"
  annotationProcessor "io.micronaut:micronaut-validation"
  compile "io.micronaut:micronaut-http-client"
  compile "io.micronaut:micronaut-http-server-netty"
  compile "io.micronaut:micronaut-inject"
  compile "io.micronaut:micronaut-validation"
  compile "io.micronaut:micronaut-runtime"
  compile 'org.apache.commons:commons-math3:3.6.1'
  compileOnly "com.oracle.substratevm:svm:GraalVM-1.0.0-rc8"
  compileOnly "io.micronaut:micronaut-inject-java"
  runtime "ch.qos.logback:logback-classic:1.2.3"
  runtime "io.micronaut:micronaut-graal"
  testCompile "junit:junit:4.12"
  testCompile "io.micronaut:micronaut-inject-java"
  testCompile "org.hamcrest:hamcrest-all:1.3"
}

task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/Dockerfile')
  //from 'openjdk:8u181-jre-slim-stretch'
  from 'openjdk:8u181-jre-alpine3.8'
  maintainer 'Domingo Suarez Torres "domingo.suarez@gmail.com"'

  copyFile "${ project.name }-${ project.version.toLowerCase() }-all.jar", '/opt/service.jar'
  exposePort 8080
  entryPoint 'java', "-Djava.awt.headless=true", "-Xms128m", "-Xmx128m", '-jar', '/opt/service.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.parentFile
  tag = 'domix/factorial-service:' + project.version.toLowerCase()
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImage
  imageName = buildImage.tag
}

createDockerfile.dependsOn assemble

shadowJar {
  mergeServiceFiles()
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "factorial.service.Application"
compileJava.options.compilerArgs += '-parameters'
compileTestJava.options.compilerArgs += '-parameters'
